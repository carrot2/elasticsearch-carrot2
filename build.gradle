buildscript {
  ext {
    localversions = [
        es : "8.5.0",
        c2 : "4.3.1"
    ]
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.elasticsearch.gradle:build-tools:${localversions.es}"
  }
}

plugins {
  id 'java-library'
  id 'idea'
  id 'com.diffplug.spotless' version "6.5.2" apply false
}

apply plugin: 'elasticsearch.esplugin'
apply plugin: 'elasticsearch.java-rest-test'
apply plugin: 'elasticsearch.yaml-rest-test'

apply from: file('gradle/validation/spotless.gradle')

// This plugin's version (typically must match that of ES).
// For bugfix releases against the same ES version, you can add a bugfix suffix.
def bugfix = ""
version = "${localversions.es}${bugfix}"
group = 'org.carrot2'

repositories {
  mavenLocal()
  mavenCentral()
}

ext {
  licenseFile = rootProject.file('LICENSE.txt')
  noticeFile = rootProject.file('NOTICE.txt')
}

esplugin {
  name 'elasticsearch-carrot2'
  description "Search results clustering plugin for Elasticsearch ${localversions.es} (Carrot2 ${localversions.c2})"
  classname 'org.carrot2.elasticsearch.ClusteringPlugin'
}

configurations {
  c2resources
}

dependencies {
  c2resources("org.carrot2:carrot2-core:${localversions.c2}", {
    transitive false
  })

  api("org.carrot2:carrot2-core:${localversions.c2}", {
    exclude group: "com.carrotsearch", module: "hppc"
  })

  testImplementation "org.assertj:assertj-core:3.13.2"

  // Let the javaRestTest see the classpath of main and tests.
  javaRestTestImplementation project.sourceSets.main.runtimeClasspath
  javaRestTestImplementation project.sourceSets.test.runtimeClasspath

  // TODO: ES 7.10.0-7.17.7 hack: missing log4j classes?
  yamlRestTestRuntimeClasspath "org.apache.logging.log4j:log4j-core:2.17.1"
}

// Set target compatibility
sourceCompatibility = 11
targetCompatibility = 11

// We don't have unit tests, only integration tests.
test.enabled = false

// Add plugin configuration files to each testClusters instance.
// 'extraConfigFile' doesn't allow directories, only files, so we
// need to add each individually
testClusters.all {
  fileTree(dir: 'src/main/config').each { file ->
    extraConfigFile 'elasticsearch-carrot2/' + file.name, file
  }
}

// Unpack and bundle the default resources with the plugin.
bundlePlugin {
  from({ zipTree(configurations.c2resources.singleFile).matching { include "**/*.utf8" } }, {
    eachFile { fcd ->
      fcd.path -= "org/carrot2/language/"
    }
    includeEmptyDirs = false
    into 'config'
  })
}

// Configure publishing.
apply from: file('gradle/publishing.gradle')

// TODO: ES 7.13.4 hack. We generate our own POM.
tasks.matching { it.path == ":validateElasticPom" }.all { Task t ->
  enabled = false
}